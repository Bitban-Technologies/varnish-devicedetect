varnishtest "Show example of per-device-class redirects."

server s1 {
       rxreq
       txresp
} -start

varnish v1 -vcl+backend {
    include "${projectdir}/../devicedetect.vcl";
    sub vcl_recv {
        call devicedetect;

        if (req.http.X-UA-Device ~ "^mobile" || req.http.X-UA-device ~ "^tablet") {
            return(synth(750, "Moved Temporarily"));
        }
    }

    sub vcl_synth {
        if (resp.status == 750) {
            set resp.http.Location = "http://m.example.com" + req.url;
            set resp.status = 302;
            return(deliver);
        }
    }

} -start

client c1 {
	txreq -req GET -url /foo -hdr "User-Agent: Mozilla/5.0 (Linux; U; Android 2.2; nb-no; HTC Desire Build/FRF91) AppleWebKit/533.1 (KHTML, like Gecko) Version/4.0 Mobile Safari/533.1" -hdr "Host: example.com"
	rxresp
	expect resp.status == 302
	expect resp.http.Location == "http://m.example.com/foo"
} -run

client c2 {
	txreq -req GET -url /foo -hdr "Host: example.com"
	rxresp
	expect resp.status == 200
	expect resp.http.Location == <undef>
} -run
